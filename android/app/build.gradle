apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

/****************************************************************************************************************************
 */
ext {
    // build & sdk version
    minSdk_version = 18
    sdk_version = 27
    buildTools_version = '27.0.1'
    // library versions
    supportLibrary_version = '27.0.2'
    supportConstraintLayout_version = '1.0.2'
    constraintLayout_version = '1.0.2'
    dagger_version = '2.11'
    retrofit_version = '2.3.0'
    gson_version = '2.8.2'
    // testing library versions
    junit_version = '4.12'
    androidTestRunner_version = '1.0.1'
    espresso_version = '3.0.1'
    javax_version = '10.0-b28'
    mockito_version = '1.10.19'
    hamcrest_version = '1.3'
    mockito_kotlin_version = '1.5.0'
    robolectric_version = '3.5.1'
}

/****************************************************************************************************************************
 */
android {
    compileSdkVersion sdk_version
    buildToolsVersion buildTools_version
    flavorDimensions "default"

    defaultConfig {
        applicationId "com.nomad5.sparkle"
        minSdkVersion minSdk_version
        targetSdkVersion sdk_version
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        test {
            java.srcDirs += "src/testShared"
        }
        androidTest {
            java.srcDirs += "src/testShared"
        }
    }
}

/****************************************************************************************************************************
 */
dependencies {
    /* Kotlin */
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    /* Default google support libraries */
    implementation "com.android.support:appcompat-v7:$supportLibrary_version"
    implementation "com.android.support:recyclerview-v7:$supportLibrary_version"
    implementation "com.android.support:design:$supportLibrary_version"
    implementation "com.android.support.constraint:constraint-layout:$supportConstraintLayout_version"

    /* Dagger used for dependency injection */
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    compileOnly "org.glassfish:javax.annotation:$javax_version"

    /* Gson used for converting POJO objects to json and back */
    implementation "com.google.code.gson:gson:$gson_version"

    /* Retrofit is used for easy access to a rest interface */
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    /* Testing */
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-all:$mockito_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version", { exclude group: 'org.jetbrains.kotlin' }
    androidTestImplementation "com.android.support.test:runner:$androidTestRunner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kotlin_version", { exclude group: 'org.jetbrains.kotlin' }
}
